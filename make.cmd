@echo off
setlocal
chcp 65001 >nul
pushd %~dp0

if /i "%1" == "release" goto RELEASE
goto :usage

:RELEASE
    if "%2"== "" goto :usage
    set version=%2

    for %%d in ("%~dp0.") do set package=%%~nxd

    echo Createing assets for "%package%"...

    :: create downloadable asset for ST4152+
    set build=4152
    set branch=st4152
    set tag=%build%-%version%
    set archive=%package%-%version%-st%build%.sublime-package
    set assets="%archive%#%archive%"
    call git tag -f %tag% %branch%
    call git archive --format zip -o "%archive%" %tag%

    :: create downloadable asset for ST4180+
    set build=4180
    set branch=master
    set tag=%build%-%version%
    set archive=%package%-%version%-st%build%.sublime-package
    set assets=%assets% "%archive%#%archive%"
    call git tag -f %tag% %branch%
    call git archive --format zip -o "%archive%" %tag%

    :: create the release
    call git push --tags --force
    gh release create --target %branch% -t "%2" "%tag%" %assets%
    del /f /q *.sublime-package
    git fetch
    goto :eof

:USAGE
    echo USAGE:
    echo.
    echo   make ^[release^]
    echo.
    echo   release ^<semver^> -- create and publish a release (e.g. 1.2.3)
    goto :eof
